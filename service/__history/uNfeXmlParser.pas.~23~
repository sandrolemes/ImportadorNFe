unit uNfeXmlParser;

interface

uses
  System.SysUtils,
  System.Classes,
  Xml.XMLDoc,
  Xml.XMLIntf,
  uDocumentoNfeDTO,
  uDocumentoNfeItemDTO;

type
  TNfeXmlParser = class
  public
    class procedure LerXml(const pArquivo: string; pDocNFeDTO: TDocumentoNfeDTO; pListaItens: TList);
  end;


implementation


uses
  uUtils;


class procedure TNfeXmlParser.LerXml(const pArquivo: string; pDocNFeDTO: TDocumentoNfeDTO; pListaItens: TList);
var
  xmlDoc: IXMLDocument;
  nodNFe: IXMLNode;
  nodInfNFe: IXMLNode;
  nodDet: IXMLNode;
  I: Integer;
  ItemDTO: TDocumentoNfeItemDTO;
begin
  xmlDoc := TXMLDocument.Create(nil);
  xmlDoc.LoadFromFile(pArquivo);
  xmlDoc.Active := True;

  nodNFe := xmlDoc.DocumentElement.ChildNodes.FindNode('NFe');
  if nodNFe = nil then
    raise Exception.Create('XML inválido: tag NFe não encontrada');

  nodInfNFe := nodNFe.ChildNodes.FindNode('infNFe');
  if nodInfNFe = nil then
    raise Exception.Create('XML inválido: tag infNFe não encontrada');

  pDocNFeDTO.CHAVE_ACESSO := uUtils.retornarApenasNumeros(nodInfNFe.Attributes['Id']);

  pDocNFeDTO.NUMERO := nodInfNFe.ChildNodes['ide'].ChildNodes['nNF'].Text;
  pDocNFeDTO.SERIE := nodInfNFe.ChildNodes['ide'].ChildNodes['serie'].Text;
  pDocNFeDTO.DT_EMISSAO := uUtils.XmlIso8601ToDateTime( nodInfNFe.ChildNodes['ide'].ChildNodes['dhEmi'].Text );
  pDocNFeDTO.EMIT_CNPJ_CPF := nodInfNFe.ChildNodes['emit'].ChildNodes.FindNode('CNPJ').Text;
  pDocNFeDTO.EMIT_RAZAO := nodInfNFe.ChildNodes['emit'].ChildNodes['xNome'].Text;

  pDocNFeDTO.DEST_CNPJ_CPF := nodInfNFe.ChildNodes['dest'].ChildNodes.FindNode('CNPJ').Text;
  pDocNFeDTO.DEST_RAZAO := nodInfNFe.ChildNodes['dest'].ChildNodes['xNome'].Text;

  pDocNFeDTO.VL_TOTAL := uUtils.XmlStrToFloat( nodInfNFe.ChildNodes['total'].ChildNodes['ICMSTot'].ChildNodes['vNF'].Text );

  for I := 0 to nodInfNFe.ChildNodes.Count - 1 do
  begin
    if nodInfNFe.ChildNodes[I].NodeName = 'det' then
    begin
      nodDet := nodInfNFe.ChildNodes[I];
      ItemDTO := TDocumentoNfeItemDTO.Create;

      ItemDTO.ITEM := StrToIntDef(nodDet.Attributes['nItem'], I + 1);
      ItemDTO.DESCRICAO := nodDet.ChildNodes['prod'].ChildNodes['xProd'].Text;
      ItemDTO.NCM := nodDet.ChildNodes['prod'].ChildNodes.FindNode('NCM').Text;
      ItemDTO.QUANTIDADE := uUtils.XmlStrToFloat( nodDet.ChildNodes['prod'].ChildNodes['qCom'].Text );
      ItemDTO.VL_UNITARIO := uUtils.XmlStrToFloat( nodDet.ChildNodes['prod'].ChildNodes['vUnCom'].Text );
      ItemDTO.VL_TOTAL := uUtils.XmlStrToFloat( nodDet.ChildNodes['prod'].ChildNodes['vProd'].Text );

      pListaItens.Add(ItemDTO);
    end;
  end;
end;

end.


